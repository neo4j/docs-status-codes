= 22NCF

== Status description
error: data exception - graph type constraint not supported. Graph type constraint definitions are not supported in the `{ <<graphTypeOperation>> }` operation.

== Explanation
This error occurs when you try to specify a graph type constraint using the `DROP` or `ALTER` operations, which does not support such operations.
Graph type constraints may only be dropped by name when using the `DROP` operation, and are completely invalid when using `ALTER`.
This also applies to graph type constraints defined inline of the graph type elements using `IS KEY` and `IS UNIQUE`.

== Example scenario

=== `ALTER` an existing constraint

Given the existing graph type:

[source,cypher]
----
ALTER CURRENT GRAPH TYPE SET {
    CONSTRAINT c1 FOR (s:Student) REQUIRE s.studentId :: INTEGER
}
----

Execute the query:

[source,cypher]
----
ALTER CURRENT GRAPH TYPE ALTER {
    CONSTRAINT c1 FOR (s:Student) REQUIRE s.studentId :: STRING
}
----

The query returns an error with GQLSTATUS 22NCF and the status description:


[source]
----
error: data exception - graph type constraint not supported. Graph type constraint definitions are not supported in the `ALTER GRAPH TYPE ALTER` operation.
----

To fix this, change the query as follows:

[source, cypher]
----
ALTER CURRENT GRAPH TYPE DROP {
    CONSTRAINT c1
}

ALTER CURRENT GRAPH TYPE ADD {
    CONSTRAINT c1 FOR (s:Student) REQUIRE s.studentId :: STRING
}
----

=== Remove a `KEY` constraint using `ALTER`

Given the existing graph type:

[source,cypher]
----
ALTER CURRENT GRAPH TYPE SET {
    ( :Person => {name :: STRING })
}
----

Execute the query:

[source,cypher]
----
ALTER CURRENT GRAPH TYPE ALTER {
    ( :Person => {name :: STRING IS KEY } )
}
----

The query returns an error with GQLSTATUS 22NCF and the status description:


[source]
----
error: data exception - graph type constraint not supported. Graph type constraint definitions are not supported in the `ALTER GRAPH TYPE ALTER` operation.
----

To fix this, change the query as follows:

[source, cypher]
----
ALTER CURRENT GRAPH TYPE ADD {
    CONSTRAINT nameForKeyConstraint FOR (p:Person) REQUIRE p.name IS KEY
}

----

=== `DROP` an undesignated constraint

For example, try to drop an undesignated constraint with an `ALTER CURRENT GRAPH TYPE DROP` query.

[source,cypher]
----
ALTER CURRENT GRAPH TYPE DROP {
    ( :Person => {name :: STRING IS KEY})
}
----

The query returns an error with GQLSTATUS 22NCF and the status description:


[source]
----
error: data exception - graph type constraint not supported. Graph type constraint definitions are not supported in the `ALTER GRAPH TYPE DROP` operation.
----

To fix this, change the query as follows:

[source, cypher]
----
ALTER CURRENT GRAPH TYPE DROP {
    CONSTRAINT nameOfKeyConstraint
}

----


ifndef::backend-pdf[]
[discrete.glossary]
== Glossary

include::partial$glossary.adoc[]
endif::[]
