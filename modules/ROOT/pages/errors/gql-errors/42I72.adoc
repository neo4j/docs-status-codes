= 42I72

== Status description
error: syntax error or access rule violation - search clause with too complex pattern.In order to have a search clause, a MATCH statement can only have a single node or relationship pattern and no selectors.

[#_example_scenarios]
== Example scenarios

For example, when trying to use a `SEARCH` clause in a `MATCH` statement with a pattern with several pattern parts:

[source,cypher]
----
MATCH (movie:Movie), ()
  SEARCH movie IN (
    VECTOR INDEX moviePlots
    FOR [1, 2, 3]
    LIMIT 5
  )
RETURN movie.title AS title
----

For example, when trying to use a `SEARCH` clause in a `MATCH` statement with a variable length:

[source,cypher]
----
MATCH (movie:Movie)-[]->{1,3}()
  SEARCH movie IN (
    VECTOR INDEX moviePlots
    FOR [1, 2, 3]
    LIMIT 5
  )
RETURN movie.title AS title
----

For example, when trying to use a `SEARCH` clause in a `MATCH` statement with a selector:

[source,cypher]
----
MATCH ANY SHORTEST ()-->(movie:Movie)
  SEARCH movie IN (
    VECTOR INDEX moviePlots
    FOR [1, 2, 3]
    LIMIT 5
  )
RETURN movie.title AS title
----

In all the above cases, you will receive an error with GQLSTATUS xref:errors/gql-errors/42001.adoc[42001].
This error has a cause with GQLSTATUS 42I72 and the status description above.

== Possible solution
The first iteration of the `SEARCH` clause comes with many restrictions on the `MATCH` statement.
It is likely that some of these restriction will be lifted in future versions of Neo4j.
For the time being, the Cypher queries under xref:errors/gql-errors/42I72.adoc#_example_scenarios[Example scenarios] can be rewritten like this:

[source,cypher]
----
MATCH (movie:Movie)
  SEARCH movie IN (
    VECTOR INDEX moviePlots
    FOR [1, 2, 3]
    LIMIT 5
  )
MATCH (movie), ()
RETURN movie.title AS title
----

[source,cypher]
----
MATCH (movie:Movie)
  SEARCH movie IN (
    VECTOR INDEX moviePlots
    FOR [1, 2, 3]
    LIMIT 5
  )
MATCH (movie)-[]->{1,3}()
RETURN movie.title AS title
----

[source,cypher]
----
MATCH (movie:Movie)
  SEARCH movie IN (
    VECTOR INDEX moviePlots
    FOR [1, 2, 3]
    LIMIT 5
  )
MATCH ANY SHORTEST ()-->(movie)
RETURN movie.title AS title
----

ifndef::backend-pdf[]
[discrete.glossary]
== Glossary

include::partial$glossary.adoc[]
endif::[]